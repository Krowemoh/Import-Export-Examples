*
   GIT.FILENAME = 'EXPORT.LINUX.FILE'
   GIT.REPO = 'https://github.com/Krowemoh/Import-Export-Examples.git'
*
   EQU TRUE TO 1
   EQU FALSE TO 0
*
* COMPILER DIRECTIVES
*
   $DEFINE DATABASE.QM
   $DEFINE PLATFORM.LINUX
*
   $IFDEF DATABASE.QM
      $CATALOGUE LOCAL
   $ENDIF
*
* %INCLUDE LINUX-USER-FILE
*
   OPEN '','LINUX-USER-FILE' TO LINUX.USER.FILE ELSE
      PRINT 'Failed to open file: LINUX-USER-FILE'
      STOP
   END
*
   DIM LINUX.USER.ITEM(12)
   MAT LINUX.USER.ITEM = ''
*
   EQU LINUX.USER.PASSWORD.ATTRIBUTE TO 1
   EQU LINUX.USER.USER.ID.ATTRIBUTE TO 2
   EQU LINUX.USER.GROUP.ID.ATTRIBUTE TO 3
   EQU LINUX.USER.GECOS.ATTRIBUTE TO 4
   EQU LINUX.USER.DIRECTORY.ATTRIBUTE TO 5
   EQU LINUX.USER.SHELL.ATTRIBUTE TO 6
   EQU LINUX.USER.LAST.CHANGED.ATTRIBUTE TO 7
   EQU LINUX.USER.MIN.AGE.ATTRIBUTE TO 8
   EQU LINUX.USER.MAX.AGE.ATTRIBUTE TO 9
   EQU LINUX.USER.WARNING.PERIOD.ATTRIBUTE TO 10
   EQU LINUX.USER.INACTIVITY.PERIOD.ATTRIBUTE TO 11
   EQU LINUX.USER.EXPIRY.DATE.ATTRIBUTE TO 12
*
   @USER1 = 'EXPORT.LINUX.FILE'
   @USER2 = 'EXPORT.LINUX.FILE'
*
* %END
*
   OPENPATH '/tmp' TO TMP.FILE ELSE
      PRINT 'Failed to open: /tmp'
      STOP
   END
*
   EXECUTE 'SELECT LINUX-USER-FILE BY USER.ID'
*
   PASSWORD.BUFFER = ''
   SHADOW.BUFFER = ''
*
   LOOP
      READNEXT ITEM.ID ELSE ITEM.ID = ''
*
   UNTIL ITEM.ID = '' DO
      MATREAD LINUX.USER.ITEM FROM LINUX.USER.FILE, ITEM.ID THEN
*
         USERNAME = ITEM.ID
*
         USER.ID = LINUX.USER.ITEM(LINUX.USER.USER.ID.ATTRIBUTE)
         GROUP.ID = LINUX.USER.ITEM(LINUX.USER.GROUP.ID.ATTRIBUTE)
*
         GECOS = LINUX.USER.ITEM(LINUX.USER.GECOS.ATTRIBUTE)
         CONVERT @VM TO ',' IN GECOS
*
         DIRECTORY = LINUX.USER.ITEM(LINUX.USER.DIRECTORY.ATTRIBUTE)
         SHELL = LINUX.USER.ITEM(LINUX.USER.SHELL.ATTRIBUTE)
*
         PASSWORD = LINUX.USER.ITEM(LINUX.USER.PASSWORD.ATTRIBUTE)
         LAST.CHANGED = LINUX.USER.ITEM(LINUX.USER.LAST.CHANGED.ATTRIBUTE)
         MIN.AGE = LINUX.USER.ITEM(LINUX.USER.MIN.AGE.ATTRIBUTE)
         MAX.AGE = LINUX.USER.ITEM(LINUX.USER.MAX.AGE.ATTRIBUTE)
         WARNING.PERIOD = LINUX.USER.ITEM(LINUX.USER.WARNING.PERIOD.ATTRIBUTE)
         INACTIVITY.PERIOD = LINUX.USER.ITEM(LINUX.USER.INACTIVITY.PERIOD.ATTRIBUTE)
         EXPIRY.DATE = LINUX.USER.ITEM(LINUX.USER.EXPIRY.DATE.ATTRIBUTE)
*
         PASSWD = ''
         PASSWD<1> = USERNAME
         PASSWD<2> = 'x'
         PASSWD<3> = USER.ID
         PASSWD<4> = GROUP.ID
         PASSWD<5> = GECOS
         PASSWD<6> = DIRECTORY
         PASSWD<7> = SHELL
*
         SHADOW = ''
         SHADOW<1> = USERNAME
         SHADOW<2> = PASSWORD
         SHADOW<3> = LAST.CHANGED
         SHADOW<4> = MIN.AGE
         SHADOW<5> = MAX.AGE
         SHADOW<6> = WARNING.PERIOD
         SHADOW<7> = INACTIVITY.PERIOD
         SHADOW<8> = EXPIRY.DATE
*
         CONVERT @AM TO ':' IN PASSWD
         CONVERT @AM TO ':' IN SHADOW
*
         PRINT PASSWD
         PRINT SHADOW
         PRINT
*
         PASSWORD.BUFFER<-1> = PASSWD
*
         IF EXPIRY.DATE = '' THEN
            SHADOW.BUFFER<-1> = SHADOW : ':'
         END ELSE
            SHADOW.BUFFER<-1> = SHADOW
         END
*
      END ELSE
         PRINT 'Failed to read ITEM.ID: ' : ITEM.ID
         STOP
      END
   REPEAT
*
   WRITE PASSWORD.BUFFER ON TMP.FILE, 'export.passwd'
   PRINT 'Exported /tmp/export.passwd'
*
   WRITE SHADOW.BUFFER ON TMP.FILE, 'export.shadow'
   PRINT 'Exported /tmp/export.shadow'
*
* END OF PROGRAM.
*
   END
*
