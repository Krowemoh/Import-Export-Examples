*
   GIT.FILENAME = 'IMPORT.LINUX.USERS'
   GIT.REPO = 'https://github.com/Krowemoh/Import-Export-Examples.git'
*
   EQU TRUE TO 1
   EQU FALSE TO 0
*
* COMPILER DIRECTIVES
*
   $DEFINE DATABASE.QM
   $DEFINE PLATFORM.LINUX
*
   $IFDEF DATABASE.QM
      $CATALOGUE LOCAL
   $ENDIF
*
* %INCLUDE LINUX-USER-FILE
*
   OPEN '','LINUX-USER-FILE' TO LINUX.USER.FILE ELSE
      PRINT 'Failed to open file: LINUX-USER-FILE'
      STOP
   END
*
   DIM LINUX.USER.ITEM(12)
   MAT LINUX.USER.ITEM = ''
*
   EQU LINUX.USER.PASSWORD.ATTRIBUTE TO 1
   EQU LINUX.USER.USER.ID.ATTRIBUTE TO 2
   EQU LINUX.USER.GROUP.ID.ATTRIBUTE TO 3
   EQU LINUX.USER.GECOS.ATTRIBUTE TO 4
   EQU LINUX.USER.DIRECTORY.ATTRIBUTE TO 5
   EQU LINUX.USER.SHELL.ATTRIBUTE TO 6
   EQU LINUX.USER.LAST.CHANGED.ATTRIBUTE TO 7
   EQU LINUX.USER.MIN.AGE.ATTRIBUTE TO 8
   EQU LINUX.USER.MAX.AGE.ATTRIBUTE TO 9
   EQU LINUX.USER.WARNING.PERIOD.ATTRIBUTE TO 10
   EQU LINUX.USER.INACTIVITY.PERIOD.ATTRIBUTE TO 11
   EQU LINUX.USER.EXPIRY.DATE.ATTRIBUTE TO 12
*
   @USER1 = 'IMPORT.LINUX.USERS'
   @USER2 = 'IMPORT.LINUX.USERS'
*
* %END
*
   OPENPATH '/etc' TO ETC.FILE ELSE
      PRINT 'Failed to open: /etc'
      STOP
   END
*
   READ PASSWD FROM ETC.FILE,'passwd' ELSE
      PRINT 'Failed to read: /etc/passwd'
      STOP
   END
*
   NUMBER.OF.LINES = DCOUNT(PASSWD,@AM)
*
   FOR I = 1 TO NUMBER.OF.LINES
      LINE = PASSWD<I>
*
      CONVERT ':' TO @AM IN LINE
      CONVERT ',' TO @VM IN LINE
*
      USERNAME = LINE<1>
*
      MAT LINUX.USER.ITEM = ''
*
      LINUX.USER.ITEM(LINUX.USER.USER.ID.ATTRIBUTE) = LINE<3>
      LINUX.USER.ITEM(LINUX.USER.GROUP.ID.ATTRIBUTE) = LINE<4>
      LINUX.USER.ITEM(LINUX.USER.GECOS.ATTRIBUTE) = LINE<5>
      LINUX.USER.ITEM(LINUX.USER.DIRECTORY.ATTRIBUTE) = LINE<6>
      LINUX.USER.ITEM(LINUX.USER.SHELL.ATTRIBUTE) = LINE<7>
*
      PRINT 'Importing passwd: ' : USERNAME
      MATWRITE LINUX.USER.ITEM ON LINUX.USER.FILE, USERNAME
   NEXT I
*
   READ SHADOW FROM ETC.FILE,'shadow' ELSE
      PRINT 'Failed to read: /etc/shadow'
      STOP
   END
*
   NUMBER.OF.LINES = DCOUNT(SHADOW,@AM)
*
   FOR I = 1 TO NUMBER.OF.LINES
      LINE = SHADOW<I>
*
      CONVERT ':' TO @AM IN LINE
      CONVERT ',' TO @VM IN LINE
*
      USERNAME = LINE<1>
*
      MATREADU LINUX.USER.ITEM FROM LINUX.USER.FILE, USERNAME THEN
*
         LINUX.USER.ITEM(LINUX.USER.PASSWORD.ATTRIBUTE) = LINE<2>
         LINUX.USER.ITEM(LINUX.USER.LAST.CHANGED.ATTRIBUTE) = LINE<3>
         LINUX.USER.ITEM(LINUX.USER.MIN.AGE.ATTRIBUTE) = LINE<4>
         LINUX.USER.ITEM(LINUX.USER.MAX.AGE.ATTRIBUTE) = LINE<5>
         LINUX.USER.ITEM(LINUX.USER.WARNING.PERIOD.ATTRIBUTE) = LINE<6>
         LINUX.USER.ITEM(LINUX.USER.INACTIVITY.PERIOD.ATTRIBUTE) = LINE<7>
         LINUX.USER.ITEM(LINUX.USER.EXPIRY.DATE.ATTRIBUTE) = LINE<8>
*
         PRINT 'Importing shadow: ' : USERNAME
         MATWRITE LINUX.USER.ITEM ON LINUX.USER.FILE, USERNAME
*
      END ELSE
         RELEASE LINUX.USER.FILE, USERNAME
         PRINT 'Failed to read user: ' : USERNAME
         STOP
      END
   NEXT I
*
   STOP
*
* END OF PROGRAM
*
   END
*
